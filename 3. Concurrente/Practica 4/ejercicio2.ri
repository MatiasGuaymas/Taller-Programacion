programa ejercicio2
areas
  AreaP1 : AreaP(5,1,5,100)
  AreaP2 : AreaP(10,1,10,100)
  AreaC1 : AreaP(11,1,11,1)
  AreaC2 : AreaP(12,1,12,1)
  Deposito : AreaC(50,50,50,50)
robots
  robot productor
  variables
    papeles:numero
    Ca:numero
    Av:numero
    terminar:boolean
  comenzar 
    papeles:=0
    Av:=PosAv
    terminar:=F
    mientras (~terminar)
      si (HayPapelEnLaEsquina)
        tomarPapel
        papeles:=papeles+1
        si (papeles=5)
          Ca:=PosCa
          BloquearEsquina(50,50)
          Pos(50,50)
          repetir 5
            depositarPapel
          Pos(Av,Ca)
          LiberarEsquina(50,50)
          papeles:=0
      sino
        si (PosCa=100)
          terminar:=V
        sino
          mover
  fin
  robot consumidor
  variables
    papeles:numero
    intentos:numero
    cant:numero
    Av:numero
  comenzar
    Av:=PosAv
    intentos:=0
    papeles:=0
    mientras (intentos<8)
      Random(cant,2,5)
      BloquearEsquina(50,50)
      Pos(50,50)
      mientras ((HayPapelEnLaEsquina)&(papeles<cant))
        tomarPapel
        papeles:=papeles+1
      si (papeles<>cant)
        repetir papeles
          depositarPapel
      Pos(Av,1)
      LiberarEsquina(50,50)
      si (papeles=cant)
        intentos:=0
        repetir papeles
          depositarPapel
      sino 
        intentos:=intentos+1
      papeles:=0
  fin
variables
  c1 :consumidor
  c2 :consumidor
  p1 :productor
  p2 :productor
comenzar
  AsignarArea(c1,AreaC1)
  AsignarArea(c2,AreaC2)
  AsignarArea(p1,AreaP1)
  AsignarArea(p2,AreaP2)
  AsignarArea(c1,Deposito)
  AsignarArea(c2,Deposito)
  AsignarArea(p1,Deposito)
  AsignarArea(p2,Deposito)
  Iniciar(c1,11,1)
  Iniciar(c2,12,1)
  Iniciar(p1,5,1)
  Iniciar(p2,10,1)
fin