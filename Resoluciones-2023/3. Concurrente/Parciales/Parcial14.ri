programa parcial14
procesos
  proceso juntarFlores(ES f: numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      f:= f+1
  fin
  proceso cuadrado(E lado: numero; ES f: numero)
  comenzar
    repetir 4
      repetir lado
        juntarFlores(f)
        mover
      derecha
  fin
  proceso depositarDeAUna(E f: numero; E av: numero; E ca: numero)
  comenzar
    repetir f
      BloquearEsquina(50,50)
      Pos(50,50)
      depositarFlor
      Pos(av, ca)
      LiberarEsquina(50,50)
  fin
  proceso recogerFlores(E f: numero)
  variables
    avIni, caIni: numero
  comenzar
    avIni:= PosAv
    caIni:= PosCa
    BloquearEsquina(50,50)
    Pos(50,50)
    repetir f
      tomarFlor
    Pos(avIni, caIni)
    LiberarEsquina(50,50)
  fin
  proceso maximo(E c1: numero; E c2: numero; E c3: numero; ES idMax: numero)
  comenzar
    si(c1 >= c2) & (c1 >= c3)
      idMax:= 1
    sino
      si (c2 >= c1) & (c2>= c3)
        idMax:= 2
      sino
        idMax:= 3
  fin
areas
  areaR1: AreaP(1,1,7,21)
  areaR2: AreaP(10,1,16,21)
  areaR3: AreaP(20,1,26,21)
  areaCo: AreaP(100,100,100,100)
  esquina: AreaC(50,50,50,50)
robots
  robot coordinador
  variables
    id, lado, f, cant1, cant2, cant3, idMax: numero
  comenzar
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)
    EnviarMensaje(3, r3)
    id:= 0
    repetir 3
      id:= id + 1
      Random(lado, 1, 6)
      si(id = 1)
        EnviarMensaje(lado, r1)
      sino
        si(id = 2)
          EnviarMensaje(lado, r2)
        sino
          EnviarMensaje(lado, r3)
    cant1:= 0
    cant2:= 0
    cant3:= 0
    repetir 9
      RecibirMensaje(id, *)
      si(id = 1)
        RecibirMensaje(f, r1)
        recogerFlores(f)
        cant1:= cant1 + f
      sino
        si(id = 2)
          RecibirMensaje(f, r2)
          recogerFlores(f)
          cant2:= cant2 + f
        sino
          RecibirMensaje(f, r3)
          recogerFlores(f)
          cant3:= cant3 + f
    maximo(cant1, cant2, cant3, idMax)
    Informar(idMax)
  fin
  robot recolector
  variables
    id, f, lado, caIni, avIni: numero
  comenzar
    RecibirMensaje(id, rf)
    RecibirMensaje(lado, rf)
    repetir 2
      f:= 0
      avIni:= PosAv
      caIni:= PosCa
      cuadrado(lado,f)
      depositarDeAUna(f, avIni, caIni)
      EnviarMensaje(id, rf)
      EnviarMensaje(f, rf)
      Pos(PosAv, caIni+lado+1)
    f:= 0
    avIni:= PosAv
    caIni:= PosCa
    cuadrado(lado,f)
    depositarDeAUna(f, avIni, caIni)
    EnviarMensaje(id,rf)
    EnviarMensaje(f,rf)
  fin
variables
  r1, r2, r3: recolector
  rf: coordinador
comenzar
  AsignarArea(r1, areaR1)
  AsignarArea(r2, areaR2)
  AsignarArea(r3, areaR3)
  AsignarArea(rf, areaCo)
  AsignarArea(r1, esquina)
  AsignarArea(r2, esquina)
  AsignarArea(r3, esquina)
  AsignarArea(rf, esquina)
  Iniciar(r1, 1,1)
  Iniciar(r2, 10,1)
  Iniciar(r3,20,1)
  Iniciar(rf, 100,100)
fin